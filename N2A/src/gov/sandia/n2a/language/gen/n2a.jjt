options
{
    STATIC = false;
    JAVA_UNICODE_ESCAPE = true;
    MULTI = true;
    VISITOR = true;
    VISITOR_EXCEPTION = "ParseException";
    NODE_DEFAULT_VOID = true;          // Only create Node classes for the #Tokens
    NODE_CLASS = "ASTNodeBase";
}

PARSER_BEGIN(ExpressionParser)

/*
Copyright 2013 Sandia Corporation.
Under the terms of Contract DE-AC04-94AL85000 with Sandia Corporation,
the U.S. Government retains certain rights in this software.
Distributed under the BSD-3 license. See the file LICENSE for details.
*/

package gov.sandia.n2a.language.gen;

import java.lang.reflect.Field;
import java.io.StringReader;

import gov.sandia.n2a.language.functions.FunctionList;
import gov.sandia.n2a.language.functions.Function;

import replete.util.ReflectionUtil;

import javax.measure.unit.*;

/**
 * This parser attempts to be a relatively simple mathematical
 * expression parser.  The parser has support for these constructs:
 *  - numeric literals, integers, longs, floats, doubles, and e notation
 *  - string literals
 *  - variables (identified by an identifier)
 *  - functions (identified by an identifier, (), and any arguments)
 *  - matrices (identified by [] and its elements)
 *  - array/matrix indexing (identified by var[idx(, idx)*])
 *  - mathematical && programming operators:
 *      mathematical: + - * / % (mod) ^ unary minus, unary plus
 *      logical: && || !
 *      equality: == !=
 *      relational: < > <= >=
 *      grouping: ()
 *  - units (for physics applications)
 * 
 * An identifier is a-zA-Z0-9 and can have _, $, and . in them.  They
 * cannot start with 0-9 nor a period (.).  They cannot end in a period.
 * They can end with any number of tick marks (single quotes, ') to identify
 * derivatives.
 * 
 * Right now the units supported are all those supported by the JScience 
 * library (SI prefixes are supported by JScience as well).  More units
 * can be added if necessary.
 *
 * This parser returns the root node of an abstract syntax tree that represents
 * the expression.
 * 
 * An expression such as "x = 3y" contains a malformed identifier "3y"
 * rather than the product "3*y".
 * An matrix expression such as "[x 5 -3]" contains two elements "x" and "5-3",
 * rather than three elements. 
 *
 * Although this parser allows functions to have any name (e.g. "foobar(3, 4)"), 
 * only those functions implemented in the application would allow a parsed
 * expression to be evaluated.  Unknown functions simply provide the tree
 * node for the function with an instance of UnknownFunction, which does not
 * evaluate.
 * 
 * Assignment operators currently require that the left hand side operand be
 * either a variable or an array access.
 * 
 * The parser is generated by the JTree extension on top of JavaCC which 
 * automatically generates the AST classes for the developer.
 * 
 * Future possibilities:
 *  - list indexing elevated to full operator, trailing any expression
 *  - Ternary operator
 */

public class ExpressionParser
{
    public static ASTNodeBase parse (String line) throws ParseException
    {
        if (line == null)
        {
            throw new IllegalArgumentException ("Line to parse cannot be null.");
        }

        // The following classes perform some obvious transformations to the AST before returning it:
        // List[A (only one node)] --> A
        // Variable[List[A B C ...]] --> Variable[A B C ...]
        // Function[List[A B C ...]] --> Function[A B C ...]

        class CollapseList implements ASTNodeTransformer
        {
            public ASTNodeBase transform (ASTNodeBase node)
            {
                if (node.getCount () == 1)
                {
                    return node.getChild (0);
                }
                return node;
            }
        }

        class MakeListIntoChildren implements ASTNodeTransformer
        {
            public ASTNodeBase transform (ASTNodeBase node)
            {
                if (node.getCount () == 1)
                {
                    ASTNodeBase list = node.getChild (0);
                    if (list instanceof ASTListNode)
                    {
                        node.clearChildren ();
                        int count = list.getCount ();
                        for (int i = 0; i < count; i++)
                        {
                            node.setChild (list.getChild (i), i);
                        }
                    }
                }
                return node;
            }
        }

        ASTTransformationContext context = new ASTTransformationContext ();
        MakeListIntoChildren l = new MakeListIntoChildren ();
        context.add (ASTFunNode .class, l);
        context.add (ASTVarNode .class, l);
        context.add (ASTListNode.class, new CollapseList ());

        try
        {
            ExpressionParser parser = new ExpressionParser (new StringReader (line + "\n"));
            Node startNode = parser.Start ();
            ASTNodeBase result = (ASTNodeBase) startNode.jjtGetChild(0);
            result = result.transform (context);
            result.setSource (line);
            return result;
        }
        catch (ParseException p)
        {
            // This is so any possible ParseException has its message
            // properly appended with the original equation text.
            ReflectionUtil.set ("detailMessage", p, p.getMessage ().trim () + "\nSource Line: " + line);
            throw p;
        }
        catch (TokenMgrError t)
        {
            ParseException p = new ParseException (t.getMessage () + "\nSource Line: " + line);
            try
            {
                Field f = Throwable.class.getDeclaredField ("cause");
                f.setAccessible (true);
                f.set (p, t);
            }
            catch (Exception e) {}
            throw p;
        }
    }
    
    private String replaceEscape(String inputStr) {
        int len = inputStr.length();
        int p = 0;
        int i;
        String metachars = "tnrbf\u005c\u005c\u005c"'";
        String chars = "\u005ct\u005cn\u005cr\u005cb\u005cf\u005c\u005c\u005c"'";

        StringBuffer output = new StringBuffer();

        while((i = inputStr.indexOf('\u005c\u005c', p)) != -1) {
            output.append(inputStr.substring(p, i));

            if(i + 1 == len)
                break;

            // find metacharacter
            char metac = inputStr.charAt(i + 1);

            // find the index of the metac
            int k = metachars.indexOf(metac);
            if(k == -1) {
                // didn't find the metachar, leave sequence as found.
                // This code should be unreachable if the parser
                // is functioning properly because strings containing
                // unknown escape characters should not be accepted.
                output.append('\u005c\u005c');
                output.append(metac);
            } else {
                // its corresponding true char
                output.append(chars.charAt(k));
            }

            // skip over both escape character & metacharacter
            p = i + 2;
        }

        // add the end of the input string to the output
        if(p < len)
            output.append(inputStr.substring(p));

        return output.toString();
    }
}
PARSER_END(ExpressionParser)

/***************************************************************
SKIP
***************************************************************/

<DEFAULT> SKIP :
{
  " "
  | "\t"
  | "\n"
  | "\r"

  | <"//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>         // Should include EOF, will add \n in input until this is fixed.
  | <"/*" (~["*"])* "*" (~["/"] (~["*"])* "*")* "/">
}


/***************************************************************
TOKENS
***************************************************************/

<DEFAULT> TOKEN:
{
    <FLOATING_POINT_LITERAL:
          (["0"-"9"])+ "." (["0"-"9"])* (<EXPONENT>)?
        | "." (["0"-"9"])+ (<EXPONENT>)?
        | (["0"-"9"])+ (<EXPONENT>)? > |
    <#EXPONENT: ["e","E"] (["+","-"])? (["0"-"9"])+> |
    <STRING_LITERAL:
        "\""
        (     (~["\"","\\","\n","\r"])
            | ("\\" ["n","t","b","r","f","\\","'","\""])
        )*
        "\"" >
}

<DEFAULT> TOKEN:
{
    <IDENTIFIER: <LETTER>(<LETTER>|<DIGIT>)*(<TICKMARK>)*> |
    <#LETTER: ["A"-"Z", "a"-"z", "_", "$", "."]> |
    <#DIGIT: ["0"-"9"] > | 
    <#TICKMARK: "'" >
}

<DEFAULT> TOKEN:
{
    < ASSIGN: <NASSIGN>|<AASSIGN>|<CASSIGN> > |

    < #NASSIGN: "="  > |
    < #AASSIGN: "+=" > |
    < #CASSIGN: ":=" > |
    
    < COMMA: ","  > |
    < SEMI:  ";"  > |
    < GT:    ">"  > |
    < LT:    "<"  > |
    < EQ:    "==" > |
    < LE:    "<=" > |
    < GE:    ">=" > |
    < NE:    "!=" > |
    < AND:   "&&" > |
    < OR:    "||" > |
    < PLUS:  "+"  > |
    < MINUS: "-"  > |
    < MUL:   "*"  > |
    < DIV:   "/"  > |
    < MOD:   "%"  > |
    < NOT:   "!"  > |
    < POWER: "^"  > |
    < LSQ:   "["  > |
    < RSQ:   "]"  > |
    < LRND:  "("  > |
    < RRND:  ")"  >
}

/* Switches to the IN_UNITS lexical state for different parsing of the unit expressions */
<DEFAULT> TOKEN:
{
    < LBRC: "{" > : IN_UNITS
}

/* Switches back to the DEFAULT lexical state for normal parsing */
<IN_UNITS> TOKEN:
{
    < RBRC: "}" > : DEFAULT
}

/* Unit expressions are allowed to be a lot less structured, as the
   parsing will be done by JScience, not this parser. Only those characters
   that could conceivably be present in unit expressions are allowed here. */
<IN_UNITS> TOKEN:
{
    < UNITS: (["A"-"Z", "a"-"z", "_", "0"-"9", ".", "(", ")", "+", "-", "*", "/", "^"])+ >
}


/***************************************************************
GRAMMAR START
***************************************************************/

ASTStart Start() #Start : {}
{
    AssignExpression() <EOF>
    {
        return jjtThis;
    }
}

void AssignExpression() : {
    Token t;
}
{    
    LOOKAHEAD(Variable() <ASSIGN>) Variable() t = <ASSIGN> CommaExpression()
    {
        jjtThis.jjtSetValue(FunctionList.get(t.image));
    } #OpNode(2)
      |
    CommaExpression()
}

void CommaExpression() #ListNode : {}
{
    OrExpression() (<COMMA> OrExpression())*
    {
    	jjtThis.jjtSetValue("LIST");
    }
}

void OrExpression() : {}
{
    AndExpression()
    (
        (<OR> AndExpression()
            {
                jjtThis.jjtSetValue(FunctionList.get(FunctionList.OP_OR));
            }
        ) #OpNode(2)
    )*
}

void AndExpression() : {}
{
    EqualExpression()
    (
        (<AND> EqualExpression()
            {
                jjtThis.jjtSetValue(FunctionList.get(FunctionList.OP_AND));
            }
        ) #OpNode(2)
    )*
}

void EqualExpression() : {}
{
    RelationalExpression()
    (
        (<NE> RelationalExpression()
            {
                jjtThis.jjtSetValue(FunctionList.get(FunctionList.OP_NE));
            }
        ) #OpNode(2)
          |
        (<EQ> RelationalExpression()
            {
                jjtThis.jjtSetValue(FunctionList.get(FunctionList.OP_EQ));
            }
        ) #OpNode(2)
    )*
}

void RelationalExpression() : {}
{
    AdditiveExpression()
    (
        (<LT> AdditiveExpression()
            {
                jjtThis.jjtSetValue(FunctionList.get(FunctionList.OP_LT));
            }
        ) #OpNode(2)
          |
        (<GT> AdditiveExpression()
            {
                jjtThis.jjtSetValue(FunctionList.get(FunctionList.OP_GT));
            }
        ) #OpNode(2)
          |
        (<LE> AdditiveExpression()
            {
                jjtThis.jjtSetValue(FunctionList.get(FunctionList.OP_LE));
            }
        ) #OpNode(2)
          |
        (<GE> AdditiveExpression()
            {
                jjtThis.jjtSetValue(FunctionList.get(FunctionList.OP_GE));
            }
        ) #OpNode(2)
    )*
}

void AdditiveExpression() : {}
{
    MultiplicativeExpression()
    (
        (<PLUS> MultiplicativeExpression()
            {
                jjtThis.jjtSetValue(FunctionList.get(FunctionList.OP_ADD));
            }
        ) #OpNode(2)
          |
        (<MINUS> MultiplicativeExpression()
            {
                jjtThis.jjtSetValue(FunctionList.get(FunctionList.OP_SUBTRACT));
            }
        ) #OpNode(2)
    )*
}

void MultiplicativeExpression() : {}
{
    UnitExpression()
    (
        (<MUL> UnitExpression()
            {
                jjtThis.jjtSetValue(FunctionList.get(FunctionList.OP_MULTIPLY));
            }
        ) #OpNode(2)
          |
        (<DIV> UnitExpression()
            {
                jjtThis.jjtSetValue(FunctionList.get(FunctionList.OP_DIVIDE));
            }
        ) #OpNode(2)
          |
        (<MOD> UnitExpression()
            {
                jjtThis.jjtSetValue(FunctionList.get(FunctionList.OP_MOD));
            }
        ) #OpNode(2)
    )*
}

/* Unary Expression With Units:
   Units act as a post-fix operator with precedence
   higher than +, -, *, /, and %, but lower than 
   ^, unary minus, unary plus, and element []. */
   
void UnitExpression() : {
    Token t;
}
{
    LOOKAHEAD(UnaryExpression() <LBRC>)
    (UnaryExpression() <LBRC> t = <UNITS> <RBRC>
        {
            jjtThis.jjtSetValue(Unit.valueOf(t.image));
        }
    ) #UnitNode
      | 
    UnaryExpression()
}

void UnaryExpression() : {}
{
    (<PLUS> PowerExpression()
        {
            jjtThis.jjtSetValue(FunctionList.get(FunctionList.OP_UPLUS));
        }
    ) #OpNode(1)
      |
    (<MINUS> PowerExpression()
        {
            jjtThis.jjtSetValue(FunctionList.get(FunctionList.OP_UMINUS));
        }
    ) #OpNode(1)
      |
    (<NOT> PowerExpression()
        {
            jjtThis.jjtSetValue(FunctionList.get(FunctionList.OP_NOT));
        }
    ) #OpNode(1)
      |
    PowerExpression()
}

// TODO: Make this rule recursive and right-associative
// In other words: a^b^c is parsed as a^(b^c)
void PowerExpression() : {}
{
    PrimaryExpression()
    [
        (<POWER> PrimaryExpression()
            {
                jjtThis.jjtSetValue(FunctionList.get(FunctionList.OP_POWER));
            }
        ) #OpNode(2)
    ]
}

void PrimaryExpression() : {}
{
    Constant()
      |
    LOOKAHEAD({getToken(1).kind == IDENTIFIER && getToken(2).kind == LRND}) Function()
      |
    Variable()
      |
    <LRND> OrExpression() <RRND> 
      |
    MatrixExpression()
}

void MatrixExpression() #MatrixNode : {
    jjtThis.jjtSetValue("MATRIX");
}
{
    <LSQ> CommaExpression() (<SEMI> CommaExpression())* <RSQ>
}

void Variable() #VarNode : {
    Token t;
}
{
    t = <IDENTIFIER> [<LSQ> CommaExpression() <RSQ>]
    {
        jjtThis.jjtSetValue(t.image);
    }
}

void Function() #FunNode : {
    Token t;
}
{
    t = <IDENTIFIER> <LRND> [CommaExpression()] <RRND>
    {
        jjtThis.jjtSetValue(FunctionList.getOrUnknown(t.image));
    }
}

void Constant() #Constant : {
    Token t;
}
{
    t = <STRING_LITERAL>
    {
        String temp = t.image.substring(1, t.image.length() - 1);
        temp = replaceEscape(temp);
        jjtThis.jjtSetValue(temp);
    }
      |
    t = <FLOATING_POINT_LITERAL>
    {
        jjtThis.jjtSetValue(Double.parseDouble(t.image));
    }
}
